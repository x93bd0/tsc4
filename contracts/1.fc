{-
  TASK 1 - Find branch of the cell tree
  Write the method that will find a branch of the tree by comparing its
  hash with the hash received in the first parameter. When the algorithm finds
  the subtree (branch) whose hash equals the received hash, the root cell of
  this branch should be returned. Return empty cell if the branch is not found.
-}

forall X -> int is_null (X x) asm "ISNULL";
() recv_internal() {
}

(cell) rec(int hash, cell tree) {
    if (cell_hash(tree) == hash) {
        return tree;
    }

    slice s = tree.begin_parse();
    repeat (s.slice_refs()) {
        cell c = rec(hash, s~load_ref());
        ifnot (is_null(c)) {
            return c;
        }
    }

    return null();
}

;; testable
(cell) find_branch_by_hash(int hash, cell tree) method_id {
    cell c = rec(hash, tree);
    ifnot (is_null(c)) {
        return c;
    }
    
    return begin_cell().end_cell();
}
