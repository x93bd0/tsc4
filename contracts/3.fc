{- 
  TASK 3 - Find and replace binary substring
  Binary string is represented as a cell linked list: string splitted to chunks,
  first chunk stored to the root cell, next one to the cell in ref and so on;
  each cell can have only one ref. 
  Write the method that find and replaces one flags in the binary string
  with another value. Flags and values can be can be of any length, but
  strictly up to 128 bits. The method must replace every flag it finds.
  Flag and the value to be replaced is guaranteed to be greater than 0.
  Lets give a simple example. We have the target flag 101110101 and the value
  to be written 111111111 as inputs, and a linked list of cells, in which the bit
  value of the first cell ends with ...10100001011, and in the ref we have cell that
  starts with 10101000111111...
  The output should be a linked list where the first
  cell ends with ...10100001111, and the second cell starts with 11111000111111...

-}

;; awa

(int) log2(int x) asm "UBITSIZE DEC";
int slice_data_empty(slice s) asm "SDEMPTY";
int slice_refs_empty(slice s) asm "SREMPTY";

() recv_internal() {
}

(cell) solve(
  slice s, int x, builder b,

  int prev, int prevs,
  int flag, int flags,
  int value, int vals
) {
  while (1) {
    if (prevs < flags) {
      int diff = min(flags - prevs, slice_bits(s));
      prevs = log2(prev) + 1;
      prev <<= diff;
      prev += s~load_uint(diff);

      if (prevs != flags) {
        if (slice_refs_empty(s)) {
          if (prevs > 1023 - x) {
            builder next = begin_cell();
            int magic = (prevs - (1023 - x));

            int v1 = prev >> magic;
            int v2 = prev - (v1 << magic);

            b~store_uint(v1, 1023 - x);
            next~store_uint(v2, magic);

            return b.store_ref(next.end_cell()).end_cell();
          }

          b~store_uint(prev, prevs);
          return b.end_cell();
        }

        s = s~load_ref().begin_parse();
      }
    } else {
      if (prev == flag) {
        prevs = 0;
        prev = 0;

        if (vals > 1023 - x) {
          builder next = begin_cell();
          int magic = (vals - (1023 - x));

          int v1 = value >> magic;
          int v2 = value - (v1 << magic);

          b~store_uint(v1, 1023 - x);
          next~store_uint(v2, magic);

          return b.store_ref(solve(
            s, magic, next,
            0, 0,
            flag, flags,
            value, vals
          )).end_cell();
        } else {
          b~store_uint(value, vals);
          x += vals;
        }
      } else {
        ;; remove every left bits
        int lbits = prev >> (flags - 1);
        b~store_uint(lbits, prevs - (flags - 1));
        prev -= lbits << (flags - 1);

        prevs = log2(prev) + 1;
        x += 1;

        if (x >= 1023) {
          return b.store_ref(solve(
            s, 0, begin_cell(),
            prev, prevs,
            flag, flags,
            value, vals
          )).end_cell();
        }
      }
    }
  }

  return b.end_cell();
}

;; testable
(cell) find_and_replace(int flag, int value, cell linked_list) method_id {
  if (flag < 0) | (value < 0) {
    return null();
  }

  int l2f = log2(flag) + 1;
  int v2f = log2(value) + 1;
  ;; TODO: See log2(flag)
  return solve(linked_list.begin_parse(), 0, begin_cell(),
    0, 0,
    flag, l2f,
    value, v2f);
}
